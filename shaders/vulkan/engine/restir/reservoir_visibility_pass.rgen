#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_shader_16bit_storage : require
#extension GL_EXT_nonuniform_qualifier : enable
#include "../raycommon.glsl"
#include "restir_structs.glsl"
#include "restir_common.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba16f) uniform image2D result;
layout(binding = 2, set = 0, rgba16f) uniform image2D normals;
layout(binding = 3, set = 0) uniform sampler baseSampler;

layout(binding = 4, set = 0) buffer AnalyticLights
{
    PackedLight SceneLights[];
};

layout(std140,binding = 5, set = 0) readonly buffer ReservoirBuffer
{
	Reservoir reservoirs[];
};

layout(std140,binding = 6, set = 0) buffer PrevReservoirBuffer
{
	Reservoir prev_reservoirs[];
};

layout(binding = 7, set = 0) uniform ShadowsProperties
{
    float LightRadius;
    float LightPenumbra;
    uint  Timestamp;
    float  LightIntensityBoost;
    uint LightsCount;
    float padd[3];
};

layout(binding = 8, set = 0) uniform SkyCfg
{
    vec4 SunDir;
    vec4 SunColor;
    vec4 HorizonColor;
    vec4 SkyColor;
};

layout(binding = 9, set = 0) buffer TriangleLights
{
    int TriLightsCount;
    int padd_2[4*3-1];
    PackedLight SceneTriLights[];
};

layout(binding = 0, set = 1) uniform CameraProperties
{
    mat4 view;
    mat4 proj;
    mat4 viewInverse;
    mat4 projInverse;
    mat4 viewProjInverse;
    mat4 viewProjInversePrev;
} cam;

// Scene descriptors
// TODO: Move to include
layout(binding = 0, set = 2, scalar) buffer ScnDesc{ sceneDesc i[]; } scnDesc;
layout(binding = 1, set = 2, scalar) buffer Vertices { Vertex v[]; } vertices[];
layout(binding = 2, set = 2) buffer Indices { uint16_t i[]; } indices[];
layout(binding = 3, set = 2) uniform texture2D textures[];
layout(binding = 4, set = 2, scalar) buffer MatDesc{ MaterialDesc i[]; } matDesc;

layout(location = 0) rayPayloadEXT ShadowHitPayload payload;

uint SmallestPowerOf2GreaterThan(in uint x)
{
    // Set all the bits behind the most significant non-zero bit in x to 1.
    // Essentially giving us the largest value that is smaller than the
    // next power of 2 we're looking for.
    x |= x >> 1;
    x |= x >> 2;
    x |= x >> 4;
    x |= x >> 8;
    x |= x >> 16;

    // Return the next power of two value.
    return x + 1;
}

float FloatPrecision(in float x, in uint NumMantissaBits)
{
    // Find the exponent range the value is in.
    uint nextPowerOfTwo = SmallestPowerOf2GreaterThan(uint(x));
    float exponentRange = nextPowerOfTwo - (nextPowerOfTwo >> 1);

    float MaxMantissaValue = 1 << NumMantissaBits;

    return exponentRange / MaxMantissaValue;
}

#include "evaluate_pdf.glsl"

// For each pixel,
// compute reservoir of random sample of size M from N lights

void main()
{
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
    vec2 d = inUV * 2.0 - 1.0;
    vec4 origin    = cam.viewInverse * vec4(0, 0, 0, 1);
    vec4 target    = cam.projInverse * vec4(d.x, d.y, 1, 1);
    vec4 direction = cam.viewInverse * vec4(normalize(target.xyz), 0);

    uint  rayFlags = gl_RayFlagsNoneEXT;
    float tMin     = 0.001;
    float tMax     = 5000.0;

    vec4 normals_d =  imageLoad(normals, ivec2(gl_LaunchIDEXT.xy));
    normals_d.xyz = normalize(normals_d.xyz);
    ivec2 tex_res = imageSize(normals);

    SurfacePoint surface;
    surface.worldPos = origin.xyz + direction.xyz * normals_d.w;
    surface.normal   = normals_d.xyz;

    uint resIndex = gl_LaunchIDEXT.x * tex_res.y + gl_LaunchIDEXT.y;
    Reservoir r = reservoirs[resIndex];

    vec3 shadow = vec3(0.0f);

    float distToLight;
    vec3 lightDir;
    float lightIntensity;
    float brdf; // lambertian term
    float g = 1.0f;
    vec3 pl_color;
    float light_bias = -0.01;
    float ignore_emissives = 1.0f;

    uint randSeed = initRand(resIndex, Timestamp, 16);

    if(r.selectedLightId < 0)
    {
        // Sun
        lightDir = SunDir.xyz;
        lightIntensity = 1.0f;
        if (lightDir.z < 0) {
            lightDir.z = -lightDir.z;
            lightIntensity = 0.15f;
        }

        lightDir = normalize(lightDir.xyz);
        distToLight = 5000.0f;

        brdf = EvaluateLambertBrdf(surface, lightDir); // lambertian term
        pl_color = vec3(lightIntensity);
        light_bias = 5;
    }
    else if(r.selectedLightId < LightsCount)
    {
        AnalyticLight pl =  UnpackLight(SceneLights[r.selectedLightId]);

        lightDir         = (pl.posAndRadius.xyz - surface.worldPos.xyz);
        distToLight      = length(lightDir);
        lightDir        /= distToLight;
        lightIntensity   = LightIntensityBoost;

        brdf = EvaluateLambertBrdf(surface, lightDir);
        g = PointLightGeometryTerm(surface, pl);
        pl_color = pl.color.rgb * lightIntensity;
        light_bias = LightRadius;
    }
    else
    {
        int triangleId = (r.selectedLightId-int(LightsCount)) % (TriLightsCount);

        TriangleLight triLight = UnpackTriLight(SceneTriLights[triangleId]);

        vec2 attribs = vec2(nextRand(randSeed),nextRand(randSeed));

        const vec3 barycentrics = vec3(1.0 - attribs.x - attribs.y, attribs.x, attribs.y);

        vec3 obj_pos = triLight.v0.xyz * barycentrics.x +
                       triLight.v1.xyz * barycentrics.y +
                       triLight.v2.xyz * barycentrics.z;
        vec4 world_pos = ((vec4(obj_pos, 1.0) * scnDesc.i[triLight.instanceId].transfo));
        float tri_area = length(cross(triLight.v1.xyz - triLight.v0.xyz,triLight.v2.xyz - triLight.v0.xyz));

        lightDir         = (world_pos.xyz - surface.worldPos.xyz);
        distToLight      = length(lightDir);
        lightDir        /= distToLight;
        lightIntensity   = LightIntensityBoost;

        brdf = EvaluateLambertBrdf(surface, lightDir);
        g = min(1.0f/(distToLight * distToLight), 1.0f);
        pl_color = vec3(lightIntensity);
		ignore_emissives = -1.0f;
    }

    payload.hitDistance = 0.0f;
    payload.velocity = 0.0f;
    payload.x = 0.0f;
    payload.y = 0.0f;
    payload.emission = vec4(0.0f);
    payload.emission.w = ignore_emissives;

    const float depth_eps = FloatPrecision(normals_d.w, 10);

    traceRayEXT(topLevelAS, rayFlags,
    0xFF, // cullMask
    0, // sbtRecordOffset
    0, // sbtRecordStride
    0, // missIndex
    surface.worldPos + depth_eps * surface.normal, // ray origin
    tMin, // ray min range
    normalize(lightDir * distToLight + vec3(nextRand(randSeed),nextRand(randSeed),nextRand(randSeed)) * max(light_bias,0.0f)), // ray direction
    max(distToLight - light_bias, 0.0f), // ray max range
    0// payload (location = 0)
    );

	vec3 emission_mul = ignore_emissives > 0 ? vec3(1.0f) : payload.emission.xyz;

    // TODO: Make treshold available from UI and a part of denoiser
    const float fireflyTreshold = 50.0f;
    shadow = (payload.hitDistance > 0 ? 1.0f: 0.0f) *
                    /*g * brdf */ pl_color * vec3(min(r.selectedLightWeight * EvaluatePDF(r.selectedLightId, surface, randSeed), fireflyTreshold)) * emission_mul/3.14f;
    r.selectedLightWeight *= (payload.hitDistance > 0 ? 1.0f: 0.0f);
    prev_reservoirs[resIndex] = r;
    imageStore(result, ivec2(gl_LaunchIDEXT.xy), vec4(shadow, payload.velocity* 100.0f));
}
